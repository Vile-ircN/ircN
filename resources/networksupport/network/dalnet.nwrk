menu status {
  $iif($nget(networksupport) == Dalnet, Usermode)
  .$iif(!$nget(collapse.dalnetmenu.status/umode),$style(1)) - Dalnet User Modes - {
    var %q = $nget(collapse.dalnetmenu.status/umode)
    nset collapse.dalnetmenu.status/umode $iif(!%q,on)
    iecho $iif(%q, Expanded, Collapsed) Status -> Usermode -> Dalnet User Modes
  }
  .$dal.popumode(C,$null, Msg Same Chans only	+C) { toggleumode C }
  .$dal.popumode(C,$null, Msg Reged only	+R) { toggleumode R }
  .$dal.popumode(r,$null, Registered	+r) { noop }
  .$un.popumode(S,S,hide, SSL Client	+S) { }

}
menu nicklist {
  $iif($nget(networksupport) == dalnet,-)
  $iif($nget(networksupport) == dalnet,&Dalnet)
  .Chanserv
  ..Op $tab +o:.chanserv op # $1
  ..De-Op $tab -o:.chanserv deop # $1
  ..Mass Kick:.chanserv mkick #
  ..Unban:.chanserv unban # $input(Unban hostmask:,e,Chanserv unban, $address($1,3))
  .-
  .Silence
  ..Add:.quote silence + $+ $address($1,3)
  ..Rem:.quote silence - $+ $address($1,3)
  ..-
  ..List:.quote silence
}
menu channel {
  $iif($nget(networksupport) == dalnet,Mode)
  . $iif($nget(networksupport) == dalnet,Dalnet Channel Modes):www http://docs.dal.net/docs/modes.html#2


}
alias -l dal.popumode {
  ;  <mode> <require> [hide] <text>
  ; checked = 1, disabled = 2, both = 3

  if ($nget(collapse.dalnetmenu.status/umode)) return 

  var %s = 1, %t
  if ($3 == hide) { var %hide = $true | set %t $4- }
  else set %t $3-
  if ($remove($2,-)) {
    var %s = $popumode.flags($iif($prop == and,AND,OR), $2)
    if (!%s) && (%hide) return
  }

  return $style( $iif($1 isincs $usermode, $iif(%s,1, 3) , $iif(!%s, 2) )) %t
}


; when this network support file is loaded on connect
on *:SIGNAL:loadnetsupport:{
  scid $1
  if ($nget(networksupport) == dalnet) {
    nset defaultservices N C M 
    nset netservices NickServ MemoServ ChanServ StatServ OperServ HelpServ
    nset defaultservices.checked  $ctime
    nset netwebsite www.DAL.net
    if (!$nget(services.nickserv.cmd))   nset services.nickserv.cmd /msg NickServ@services.dal.net
    if (!$nget(services.chanserv.cmd))  nset services.chanserv.cmd /msg ChanServ@services.dal.net
    if (!$nget(services.memoserv.cmd))  nset services.memoserv.cmd /msg MemoServ@services.dal.net

    ncid can_dccallow $true
    ncid can_silence $true
  }
  scid -r
}
; when this network support file is unloaded when they switch to something else
on *:SIGNAL:unloadnetsupport:{
  scid $1
  if ($nget(networksupport) == dalnet) {
    nset defaultservices
    nset defaultservices.checked
    nset netservices  
    ncid can_dccallow
    ncid can_silence  
  }
  scid -r
}
